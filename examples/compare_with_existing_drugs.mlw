module Compare_with_existing_drugs
  use cameleerBHL.CameleerBHL
  use ttest.Ttest
  use real.RealInfix

  let function mu_new   : parameter = "mu_new"
  let function mu_drug1 : parameter = "mu_drug1"
  let function mu_drug2 : parameter = "mu_drug2"
  let function v        : parameter = "var"

  let function ppl_new   : distribution = NormalD (Param mu_new) (Param v)
  let function ppl_drug1 : distribution = NormalD (Param mu_drug1) (Param v)
  let function ppl_drug2 : distribution = NormalD (Param mu_drug2) (Param v)

  let function h_new_drug1   : formula = mean ppl_new $> mean ppl_drug1
  let function h_new_drug1_c : formula = mean ppl_new $< mean ppl_drug1
  let function h_new_drug2   : formula = mean ppl_new $> mean ppl_drug2
  let function h_new_drug2_c : formula = mean ppl_new $< mean ppl_drug2
  let function h1            : formula = h_new_drug1 $|| h_new_drug2

  let cmp_with_existing_drugs (d_new d_drug1 d_drug2 : dataset real) : real
    requires {
      is_empty !st /\
      sampled d_new ppl_new /\
      d_new.scale = Interval /\ d_drug1.scale = Interval /\ d_drug2.scale = Interval /\
      sampled d_drug1 ppl_drug1 /\ sampled d_drug2 ppl_drug2 /\
      independent d_new d_drug1 /\ independent d_new d_drug2 /\
      (World !st interp) |= Possible h_new_drug1 /\
      (World !st interp) |= Not (Possible h_new_drug1_c) /\
      (World !st interp) |= Possible h_new_drug2 /\
      (World !st interp) |= Not (Possible h_new_drug2_c)
    }
    ensures {
      let p = result in
      (Leq p) = compose_pvs h1 !st &&
      (World !st interp) |= StatB (Leq p) h1
    }
  =
    let p_drug1 = exec_ttest_ind_eq ppl_new ppl_drug1 d_new d_drug1 Up in
    let p_drug2 = exec_ttest_ind_eq ppl_new ppl_drug2 d_new d_drug2 Up in
    p_drug1 +. p_drug2
end
