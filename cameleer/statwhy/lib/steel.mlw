module Steel
  use cameleerBHL.CameleerBHL
  use int.ComputerDivision
  use list.Combine
  use array.Array

  let function (+::) (xs : list 'a) (x : 'a) : list 'a = Cons x xs

  let function div2 (r : int) : int = div r 2

  function cmb_store (cmbs : list formula) (ps : array real) (i : int) : store
  = match cmbs with
    | Cons f fs -> Cons ("steel", f, Eq (ps[i])) (cmb_store fs ps (i + 1))
    | Nil -> Nil
    end
  
  val exec_steel (dists : list distribution) (control_dist : distribution)
                 (ys : list (dataset real)) (c : dataset real)
                 (alt : alternative) : array real
  writes { st }
  requires {
    Length.length dists = Length.length ys /\
    for_all2 sampled ys dists /\ sampled c control_dist /\
    for_all (fun y -> scale_leq Ordinal y.scale) ys /\ scale_leq Ordinal c.scale /\
    for_all (fun p -> (World !st interp) |= eq_variance control_dist p) dists /\
    match alt with
    | Two -> for_all
               (fun p ->
                 (World !st interp) |= Possible (mean control_dist $< mean p) /\
                 (World !st interp) |= Possible (mean control_dist $> mean p))
               dists
    | Up -> for_all
              (fun p ->
                (World !st interp) |= Possible (mean control_dist $> mean p) /\
                (World !st interp) |= Not (Possible (mean control_dist $< mean p)))
              dists
    | Low -> for_all
               (fun p ->
                 (World !st interp) |= Not (Possible (mean control_dist $> mean p)) /\
                 (World !st interp) |= Possible (mean control_dist $< mean p))
             dists
    end
  } ensures {
    let ps = result in
    length ps = Length.length dists /\
    let b = length ps in
    (forall i : int. 0 <= i < b -> pvalue (ps[i])) /\
    let cmbs = map (fun t -> match alt with
                             | Two -> mean control_dist $!= mean t
                             | Up -> mean control_dist $> mean t
                             | Low -> mean control_dist $< mean t
                             end)
                   dists in
    !st = (cmb_store cmbs ps 0) ++ !(old st)
  }
end
